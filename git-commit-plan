#!/bin/zsh

# |---------------------------|
# |        Commit-Plan        |
# |---------------------------|

BRANCH=$(git symbolic-ref --short HEAD)

header(){
    echo
    echo "Branch: $BRANCH"
    echo
    echo "Commit-Plan:"
    echo
}

serializeCommitMessages() {
    local plans=("${(@)@}")
    local trimmed=("${plans[@]%%[[:space:]]##}")
    local underscored=("${trimmed[@]// /_}")
    echo "${(j:%:)underscored}"
}

showCommitPlan(){
    commit_data=$(git config --get branch.$BRANCH.commit-plan)
    local commit_plans=("${(s:%:)commit_data}")
    for plan in "${commit_plans[@]}"; do
        echo " => ${plan//_/ }"
    done
}

createCommitPlan() {
    echo " - Add your Commit messages."
    echo " - Press <ENTER> to continue."
    echo " - Enter an empty message to Finish."
    echo

    commit_plans=()
    while true; do
        echo -n " => "
        read -r plan < /dev/tty
        if [[ -z "$plan" ]]; then
            # if plan is empty
            break;
        else
            commit_plans+=("$plan")
        fi
    done

    commit_data=$(serializeCommitMessages "${commit_plans[@]}")
    git config "branch.$BRANCH.commit-plan" "$commit_data"
    echo "    commit-plan created\n"
}

addCommitMessage(){
    commit_data=$(git config --get branch.$BRANCH.commit-plan)
    new_message=$1
    commit_data=$(serializeCommitMessages "$commit_data" "$new_message")
    git config "branch.$BRANCH.commit-plan" "$commit_data"
}

deleteCommitMessage(){
    # show messages with numbers
    showCommitPlan | sed '=' | sed 'N;s/\n/ /'
    echo
    echo -n "Select a message(#) to delete: "
    read -r message_number < /dev/tty

    commit_data=$(git config --get branch.$BRANCH.commit-plan)
    local commit_plans=("${(s:%:)commit_data}")

    if [[ $message_number -lt 1 || $message_number -gt ${#commit_plans[@]} ]]; then
        echo "Invalid message number."
    fi

    local deleted_message="${commit_plans[message_number]}"
    commit_plans[$message_number]=()
    commit_data="${(j:%:)commit_plans}"
    git config "branch.$BRANCH.commit-plan" "$commit_data"

    echo "Deleted: ${deleted_message//_/ }"
    echo
}


commitChanges() {
    if git diff --cached --quiet; then
        echo "No staged changes."
        exit 0
    fi

    header
    showCommitPlan | sed '=' | sed 'N;s/\n/ /'
    echo
    echo -n "Select a message(#) to commit: "
    read -r message_number < /dev/tty

    commit_data=$(git config --get branch.$BRANCH.commit-plan)
    local commit_plans=("${(s:%:)commit_data}")

    git commit -m "${commit_plans[message_number]//_/ }"
    # if Commit was successful
    if [[ $? -eq 0 ]]; then
        commit_plans[$message_number]=()
        commit_data="${(j:%:)commit_plans}"
        git config "branch.$BRANCH.commit-plan" "$commit_data"
    fi
}


# |----------------------|
# |        main()        |
# |----------------------|

    #   no arguments
    # ----------------

if [[ $# -eq 0 ]]; then
    # if commit-plan already exists
    if git config --get branch.$BRANCH.commit-plan >/dev/null 2>&1; then
        header
        showCommitPlan
        echo
    else # does not exist
        header
        createCommitPlan
        echo
    fi
    exit 0
fi

    #   arguments
    # -------------

while [[ $# -gt 0 ]]; do
    case "$1" in
        --help | -h)
            header
            echo "Usage: git commit-plan [options]"
            echo "options:"
            echo " -n| --new               Create a new commit plan."
            echo " -c| --commit            Commit staged changes."
            echo " -m| --message [message] Add a Message to the commit plan."
            echo " -d| --delete            Remove a Message from the commit plan."
            echo " -h| --help              Show this help message."
            echo
            exit 0
            ;;
        --new | -n)
            header
            createCommitPlan
            echo
            exit 0
            ;;
        --message | -m)
            header
            addCommitMessage "$2"
            exit 0
            ;;
        --delete | -d)
            header
            deleteCommitMessage
            exit 0
            ;;
        --commit | -c)
            commitChanges
            exit 0
            ;;
    esac
    shift
done
